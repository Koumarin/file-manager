[gd_scene load_steps=2 format=2]

[sub_resource type="GDScript" id=1]
script/source = "extends Tree

signal directory_selected(path)

var directory := Directory.new()

func _ready() -> void:
	var item: TreeItem
	
	connect('item_activated', self, '_on_item_activated')
	connect('item_collapsed', self, '_on_item_collapsed')
	
	set_hide_root(true)
	item = create_item()
	item.set_text(0, 'Filesystem')
	item.set_selectable(0, false)
	
	for i in range(directory.get_drive_count()):
		var path := get_drive(i)
		var subdirs: Array
		
		item = create_item()
		item.set_text(0, Util.translate_path_name(path))
		item.set_metadata(0, path)
		item.collapsed = true
		
		update_subdirectories(item)

func update_subdirectories(item: TreeItem) -> void:
	var path: String = cascade_tree(item)
	var subdirs     := Util.get_subdirectories(path)
	
	## If we just got here from a Tree signal, it's most likely that
	## our tree is blocked and we can't add children, so we wait a
	## frame until the tree unblocks.
	yield(get_tree(), 'idle_frame')
	for dir in subdirs:
		var subitem := create_item(item)
		
		if subitem:
			subitem.set_text(0, Util.translate_path_name(dir))
			subitem.set_metadata(0, dir)
			subitem.collapsed = true

func get_drive(idx: int) -> String:
	var path := directory.get_drive(idx)
	
	match OS.get_name():
		'Windows': return path + '/'
		_:         return path

func cascade_tree(item: TreeItem) -> String:
	var path = item.get_metadata(0)
	
	if not path:
		return ''
	else:
		var delim := '' if path.ends_with('/') else '/'
		return cascade_tree(item.get_parent()) + delim + item.get_metadata(0)

func _on_item_activated() -> void:
	var selected_node = get_selected()
	
	if null == selected_node:
		return
	emit_signal('directory_selected', cascade_tree(selected_node))

### This triggers both when an item collapses and when it's uncollapsed.
func _on_item_collapsed (item: TreeItem) -> void:
	if not item.collapsed:
		var path: String = item.get_metadata(0)
		var i:    TreeItem
		
		i = item.get_children()
		while i:
			update_subdirectories(i)
			i = i.get_next()
"

[node name="FileTree" type="Tree"]
margin_right = 253.0
margin_bottom = 600.0
mouse_filter = 1
size_flags_horizontal = 3
size_flags_vertical = 3
script = SubResource( 1 )
